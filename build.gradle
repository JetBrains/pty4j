import java.text.SimpleDateFormat
import org.apache.tools.ant.filters.FixCrLfFilter

plugins {
  id 'java-library'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'maven-publish'
}

apply plugin: 'maven-publish'

def pathToNativeInJar = 'resources/com/pty4j/native'
def projectVersion = new File(rootProject.projectDir, 'VERSION').text

version = projectVersion

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
    compileClasspath += files('lib/purejavacomm.jar')
  }
  test {
    java {
      srcDirs = ['test']
    }
  }
}

compileJava {
  sourceCompatibility = '1.5'
  targetCompatibility = '1.5'
  options.debugOptions.debugLevel = 'lines,vars,source'
}

repositories {
  mavenCentral()
  flatDir dirs: ['lib']
}

jar {
  from('os') {
    include '**/*'
    into pathToNativeInJar
  }
  manifest {
    attributes(
        'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
        'Created-By': "Gradle ${gradle.gradleVersion}",
        'Build-Jdk': "${System.properties['java.runtime.version']}",
        'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    )
  }
}

task sourcesZip(type: Jar, dependsOn: classes) {
  classifier 'source'
  baseName 'pty4j'
  extension 'zip'
  from sourceSets.main.allSource
  filter(FixCrLfFilter.class, eol:FixCrLfFilter.CrLf.newInstance("lf"))
}

artifacts {
  archives sourcesZip
}

task testJar(type: Test, dependsOn: [jar, testClasses]) {
  description = 'Runs tests on built jar instead of build/classes/java/main/**/*.class files'
  group = 'verification'

  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = project.files("$buildDir/libs/" + jar.archiveName,
                            sourceSets.test.output.classesDirs,
                            configurations.testRuntimeClasspath)
  systemProperty 'pty4j.preferred.native.folder', 'false'
  shouldRunAfter test
}

check.dependsOn test, testJar

dependencies {
  implementation 'com.github.purejavacomm:purejavacomm:1.0.2.RELEASE'
  implementation 'org.jetbrains:annotations:16.0.2'
  implementation 'com.google.guava:guava:14.0.1'
  implementation "log4j:log4j:1.2.14"
  implementation 'net.java.dev.jna:jna:4.1.0'
  implementation 'net.java.dev.jna:jna-platform:4.1.0'
  testImplementation 'junit:junit:4.12'
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'org.jetbrains.pty4j'
      artifactId 'pty4j'
      version projectVersion
      artifact sourcesZip
      pom {
        name = 'pty4j'
        description = 'Pseudo terminal(PTY) implementation in Java'
        url = 'https://github.com/traff/pty4j'
        licenses {
          license {
            name = 'Eclipse Public License 1.0'
            url = 'https://opensource.org/licenses/eclipse-1.0.php'
          }
        }
        developers {
          developer {
            id = 'JetBrains'
            name = 'JetBrains Team'
            organization = 'JetBrains'
            organizationUrl = 'https://www.jetbrains.com'
            email = 'dmitry.trofimov@jetbrains.com'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:JetBrains/pty4j.git'
          developerConnection = 'scm:git:ssh:github.com/JetBrains/pty4j.git'
          url = 'https://github.com/JetBrains/pty4j'
        }
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'pty4j'
    name = 'org.jetbrains.pty4j:pty4j'
    userOrg = 'jetbrains'
    licenses = ['EPL-1.0']
    vcsUrl = 'https://github.com/JetBrains/pty4j.git'

    version {
      name = projectVersion
    }
  }
  publications = ['MyPublication']
}
