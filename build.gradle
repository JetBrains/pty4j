import java.text.SimpleDateFormat
import org.apache.tools.ant.filters.FixCrLfFilter
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import jetbrains.sign.GpgSignSignatoryProvider

buildscript {
  repositories {
    maven { url 'https://packages.jetbrains.team/maven/p/jcs/maven' }
  }
  dependencies {
    classpath 'com.jetbrains:jet-sign:38'
  }
}

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

def pathToNativeInJar = 'resources/com/pty4j/native'
def projectVersion = new File(rootProject.projectDir, 'VERSION').text

version = projectVersion
group = 'org.jetbrains.pty4j'
archivesBaseName = 'pty4j'

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
  }
  test {
    java {
      srcDirs = ['test']
    }
    resources {
      srcDir 'test/resources'
    }
  }
}

test {
  testLogging {
    events("passed", "skipped", "failed")
    showStackTraces = true
    exceptionFormat = TestExceptionFormat.FULL
    showStandardStreams = true
  }
}

compileJava {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
  options.debugOptions.debugLevel = 'lines,vars,source'
}

repositories {
  maven {
    url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
  }
  mavenCentral()
}

jar {
  from('os') {
    include '**/*'
    into pathToNativeInJar
  }
  manifest {
    attributes(
        'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
        'Created-By': "Gradle ${gradle.gradleVersion}",
        'Build-Jdk': "${System.properties['java.runtime.version']}",
        'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    )
  }
}

task sourcesZip(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
  filter(FixCrLfFilter.class, eol:FixCrLfFilter.CrLf.newInstance("lf"))
}

task javadocJar(type: Jar) {
  from javadoc
}

artifacts {
  archives sourcesZip
}

task testJar(type: Test, dependsOn: [jar, testClasses]) {
  description = 'Runs tests on built jar instead of build/classes/java/main/**/*.class files'
  group = 'verification'

  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = project.files("$buildDir/libs/" + jar.archiveFileName.get(),
                            sourceSets.test.output.classesDirs,
                            sourceSets.test.output.resourcesDir,
                            configurations.testRuntimeClasspath)
  systemProperty 'pty4j.preferred.native.folder', 'false'
  shouldRunAfter test
}

check.dependsOn test, testJar

dependencies {
  implementation 'org.jetbrains.pty4j:purejavacomm:0.0.11.1'
  implementation 'org.jetbrains:annotations:20.1.0'
  implementation "org.slf4j:slf4j-api:1.7.36"
  implementation 'net.java.dev.jna:jna:5.12.1'
  implementation 'net.java.dev.jna:jna-platform:5.12.1'
  testImplementation 'com.google.guava:guava:31.1-jre'
  testImplementation 'junit:junit:4.13.2'
  testRuntimeOnly "org.slf4j:slf4j-simple:1.7.36"
}

ext.publishingUser = System.getenv('PUBLISHING_USER')
ext.publishingPassword = System.getenv('PUBLISHING_PASSWORD')

nexusPublishing {
  repositories {
    sonatype {
      username = rootProject.ext.publishingUser
      password = rootProject.ext.publishingPassword
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      group rootProject.group
      artifactId archivesBaseName
      artifact sourcesZip
      artifact javadocJar {
        classifier 'javadoc'
      }
      version rootProject.ext.publishingUser != null ? projectVersion + '-jdk8' : projectVersion + '-jdk8-SNAPSHOT'
      pom {
        name = 'pty4j'
        description = 'Pseudo terminal(PTY) implementation in Java'
        url = 'https://github.com/JetBrains/pty4j/tree/1.8'
        licenses {
          license {
            name = 'Eclipse Public License 1.0'
            url = 'https://opensource.org/licenses/eclipse-1.0.php'
          }
        }
        developers {
          developer {
            id = 'sergey.simonchik'
            name = 'Sergey Simonchik'
            organization = 'JetBrains'
            organizationUrl = 'https://www.jetbrains.com'
            email = 'sergey.simonchik@jetbrains.com'
          }
          developer {
            id = 'dmitry.trofimov'
            name = 'Dmitry Trofimov'
            organization = 'JetBrains'
            organizationUrl = 'https://www.jetbrains.com'
            email = 'dmitry.trofimov@jetbrains.com'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:JetBrains/pty4j.git'
          developerConnection = 'scm:git:ssh:github.com/JetBrains/pty4j.git'
          url = 'https://github.com/JetBrains/pty4j'
        }
      }
    }
  }
}

signing {
  sign publishing.publications
  signatories = new GpgSignSignatoryProvider()
}
